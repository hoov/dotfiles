# hoov prompt theme

prompt_hoov_help () {
  cat <<'EOF'

  prompt hoov [<promptcolor> [<usercolor> [<rootcolor>]]]

  defaults are blue, green, and red, respectively.

EOF
}

prompt_hoov_setup () {
  prompt_hoov_prompt=${1:-'blue'}
  prompt_hoov_user=${2:-'green'}
  prompt_hoov_root=${3:-'red'}
  
  if [ "$USER" = 'root' ]
  then
    base_prompt="%F{$prompt_hoov_root}%m%k%f "
  else
    base_prompt="%F{$prompt_hoov_user}%n@%m%k%f "
  fi
  post_prompt="%b%f%k"
  
  
  path_prompt="%F{$prompt_hoov_prompt}%1~"
  PS1='$base_prompt$vcs_info_msg_0_$path_prompt %# $post_prompt'
  PS2='$base_prompt$vcs_info_msg_0_$path_prompt %_> $post_prompt'
  PS3='$base_prompt$vcs_info_msg_0_$path_prompt ?# $post_prompt'
  
  autoload -Uz vcs_info
  add-zsh-hook precmd prompt_hoov_precmd
  
  # check-for-changes can be really slow.
  # you should disable it, if you work with large repositories    
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' unstagedstr '¹'  # display ¹ if there are unstaged changes
  zstyle ':vcs_info:*' stagedstr '²'    # display ² if there are staged changes
  zstyle ':vcs_info:*' actionformats '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{3}|%F{1}%a%F{5}]%f '
  zstyle ':vcs_info:*' formats       '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%u%c%F{5}]%f '
}

prompt_hoov_precmd () {
  setopt noxtrace noksharrays localoptions
  local exitstatus=$?
  local git_dir git_ref
    
  psvar=()
  [[ $exitstatus -ge 128 ]] && psvar[1]=" $signals[$exitstatus-127]" ||
    psvar[1]=""
        
  [[ -o interactive ]] && jobs -l
    
  vcs_info
}

prompt_hoov_setup "$@"

